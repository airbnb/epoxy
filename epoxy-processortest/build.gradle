import org.gradle.internal.jvm.Jvm

apply plugin: 'com.android.library'
apply from: 'build.workaround-missing-resource.gradle'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
  compileSdkVersion rootProject.COMPILE_SDK_VERSION

  defaultConfig {
    minSdkVersion rootProject.MIN_SDK_VERSION
    targetSdkVersion rootProject.TARGET_SDK_VERSION
  }

  dataBinding {
    enabled = true
  }
}

dependencies {
  compile rootProject.deps.kotlin
  compile project(':epoxy-adapter')
  compile project(':epoxy-databinding')
  compile project(':epoxy-processortest2')

  kapt project(':epoxy-processor')
  // Need to include the processor directly since we create an instance of it in code for testing
  testCompile project(':epoxy-processor')

  compile rootProject.deps.paris
  kapt rootProject.deps.parisProcessor
  // Need to include the processor directly since we create an instance of it in code for testing
  testCompile rootProject.deps.parisProcessor

  kapt rootProject.deps.dataBindingCompiler
  kapt rootProject.deps.autoValue

  testCompile files(getRuntimeJar())
  testCompile files(Jvm.current().getToolsJar())
  testCompile rootProject.deps.googleTestingCompile
}

static def getRuntimeJar() {
  try {
    final File javaBase = new File(System.getProperty("java.home")).getCanonicalFile()
    File runtimeJar = new File(javaBase, "lib/rt.jar")
    if (runtimeJar.exists()) {
      return runtimeJar
    }
    runtimeJar = new File(javaBase, "jre/lib/rt.jar")
    return runtimeJar.exists() ? runtimeJar : null
  } catch (IOException e) {
    throw new RuntimeException(e)
  }
}

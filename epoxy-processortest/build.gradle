import org.gradle.internal.jvm.Jvm

apply plugin: 'com.android.library'
apply from: 'build.workaround-missing-resource.gradle'

android {
  compileSdkVersion rootProject.COMPILE_SDK_VERSION
  buildToolsVersion rootProject.ANDROID_BUILD_TOOLS_VERSION

  defaultConfig {
    minSdkVersion rootProject.MIN_SDK_VERSION
    targetSdkVersion rootProject.TARGET_SDK_VERSION
  }

  dataBinding {
    enabled = true
  }
}

dependencies {
  testCompile project(':epoxy-processor')
  testCompile project(':epoxy-databinding')
  testCompile project(':epoxy-processortest2')
  testCompile files(getRuntimeJar())
  testCompile files(Jvm.current().getToolsJar())
  testCompile rootProject.deps.googleTestingCompile
}

static def getRuntimeJar() {
  try {
    final File javaBase = new File(System.getProperty("java.home")).getCanonicalFile();
    File runtimeJar = new File(javaBase, "lib/rt.jar");
    if (runtimeJar.exists()) {
      return runtimeJar;
    }
    runtimeJar = new File(javaBase, "jre/lib/rt.jar");
    return runtimeJar.exists() ? runtimeJar : null;
  } catch (IOException e) {
    throw new RuntimeException(e);
  }
}

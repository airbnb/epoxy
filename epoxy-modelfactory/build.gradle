import org.gradle.internal.jvm.Jvm

apply plugin: 'com.android.library'
apply from: 'build.workaround-missing-resource.gradle'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
  compileSdkVersion rootProject.COMPILE_SDK_VERSION

  defaultConfig {
    minSdkVersion rootProject.MIN_SDK_VERSION
    targetSdkVersion rootProject.TARGET_SDK_VERSION
  }
}

configurations.all { strategy ->
  strategy.resolutionStrategy.force rootProject.deps.junit, rootProject.deps.robolectric,
      rootProject.deps.mockito
}

dependencies {
  compile project(':epoxy-adapter')
  compile rootProject.deps.paris

  testCompile project(':epoxy-adapter')
  // Need to include the processor directly since we create an instance of it in code for testing
  testCompile project(':epoxy-processor')
  testCompile project(':epoxy-processortest')
  testCompile rootProject.deps.googleTestingCompile
  testCompile rootProject.deps.junit
  testCompile rootProject.deps.kotlin
  // Need to include the processor directly since we create an instance of it in code for testing
  testCompile rootProject.deps.parisProcessor
  testCompile rootProject.deps.robolectric
  testCompile files(getRuntimeJar())
  testCompile files(Jvm.current().getToolsJar())

  kaptTest project(':epoxy-processor')
}

static def getRuntimeJar() {
  try {
    final File javaBase = new File(System.getProperty("java.home")).getCanonicalFile()
    File runtimeJar = new File(javaBase, "lib/rt.jar")
    if (runtimeJar.exists()) {
      return runtimeJar
    }
    runtimeJar = new File(javaBase, "jre/lib/rt.jar")
    return runtimeJar.exists() ? runtimeJar : null
  } catch (IOException e) {
    throw new RuntimeException(e)
  }
}

apply from: rootProject.file('gradle/gradle-maven-push.gradle')

import org.gradle.internal.jvm.Jvm

apply plugin: 'com.android.library'
apply from: 'build.workaround-missing-resource.gradle'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
  compileSdkVersion rootProject.COMPILE_SDK_VERSION

  defaultConfig {
    minSdkVersion rootProject.MIN_SDK_VERSION
    targetSdkVersion rootProject.TARGET_SDK_VERSION
  }
}

configurations.all { strategy ->
  strategy.resolutionStrategy.force rootProject.deps.junit, rootProject.deps.robolectric,
      rootProject.deps.mockito
}

dependencies {
  implementation project(':epoxy-adapter')
  implementation rootProject.deps.paris

  testImplementation project(':epoxy-adapter')
  // Need to include the processor directly since we create an instance of it in code for testing
  testImplementation project(':epoxy-processor')
  testImplementation project(':epoxy-processortest')
  testImplementation rootProject.deps.androidRecyclerView

  testImplementation rootProject.deps.googleTestingCompile
  testImplementation rootProject.deps.junit
  testImplementation rootProject.deps.kotlin
  // Need to include the processor directly since we create an instance of it in code for testing
  testImplementation rootProject.deps.parisProcessor
  testImplementation rootProject.deps.robolectric
  testImplementation files(getRuntimeJar())
  testImplementation files(Jvm.current().getToolsJar())

  kaptTest project(':epoxy-processor')
}

// Javadoc isn't working well with Kotlin :(
tasks.withType(Javadoc).all { enabled = false }

static def getRuntimeJar() {
  try {
    final File javaBase = new File(System.getProperty("java.home")).getCanonicalFile()
    File runtimeJar = new File(javaBase, "lib/rt.jar")
    if (runtimeJar.exists()) {
      return runtimeJar
    }
    runtimeJar = new File(javaBase, "jre/lib/rt.jar")
    return runtimeJar.exists() ? runtimeJar : null
  } catch (IOException e) {
    throw new RuntimeException(e)
  }
}

apply from: rootProject.file('gradle/gradle-maven-push.gradle')

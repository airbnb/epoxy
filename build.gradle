// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {

  ext.KOTLIN_VERSION = "1.3.61"
  ext.ANDROID_PLUGIN_VERSION = '3.5.3'

  repositories {
    google()
    jcenter()
    // For gradle-maven-publish-plugin snapshot
    maven { url "https://oss.sonatype.org/service/local/repositories/snapshots/content/" }
  }
  dependencies {
    classpath "com.android.tools.build:gradle:$ANDROID_PLUGIN_VERSION"
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$KOTLIN_VERSION"
    // Upload with:
    // ./gradlew clean assemble androidSourcesJar androidJavadocsJar uploadArchives --no-daemon --no-parallel
    // Need to use snapshot version and explicitly include javadoc/sources tasks until
    // https://github.com/vanniktech/gradle-maven-publish-plugin/issues/54 is fixed
    classpath 'com.vanniktech:gradle-maven-publish-plugin:0.9.0-SNAPSHOT'
  }
}

plugins {
  id 'com.github.ben-manes.versions' version '0.20.0'
}

allprojects {

  repositories {
    google()
    jcenter()
  }

  // Prevent javadoc task complaining about errors with kotlin files
  tasks.withType(Javadoc) {
    excludes = ['**/*.kt']
  }
}

subprojects { project ->
  apply from: "$rootDir/blessedDeps.gradle"
  apply plugin: 'com.github.ben-manes.versions'
  apply plugin: 'checkstyle'
  apply from: "${project.rootDir}/ktlint.gradle"

  task checkstyle(type: Checkstyle) {
    configFile rootProject.file('checkstyle.xml')
    source 'src/main/java'
    ignoreFailures false
    showViolations true
    include '**/*.java'

    classpath = files()
  }

  afterEvaluate {
    if (project.tasks.findByName('check')) {
      check.dependsOn('checkstyle')
      check.dependsOn('ktlint')
    }
  }
}

def isCi() {
  project.hasProperty('CI') && CI.equals('true')
}

def isReleaseBuild() {
  return !VERSION_NAME.contains("SNAPSHOT")
}

def getReleaseRepositoryUrl() {
  return hasProperty('RELEASE_REPOSITORY_URL') ? RELEASE_REPOSITORY_URL
      : "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
}

def getSnapshotRepositoryUrl() {
  return hasProperty('SNAPSHOT_REPOSITORY_URL') ? SNAPSHOT_REPOSITORY_URL
      : "https://oss.sonatype.org/content/repositories/snapshots/"
}

def getRepositoryUsername() {
  return hasProperty('NEXUS_USERNAME') ? NEXUS_USERNAME : ""
}

def getRepositoryPassword() {
  return hasProperty('NEXUS_PASSWORD') ? NEXUS_PASSWORD : ""
}

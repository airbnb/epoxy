import com.android.build.gradle.internal.LoggerWrapper
import com.android.build.gradle.internal.SdkHandler
import org.gradle.internal.jvm.Jvm

apply plugin: 'kotlin'

configurations.all { strategy ->
    strategy.resolutionStrategy.force rootProject.deps.assertj, rootProject.deps.googleTestingCompile
}

// Hack to allow us to use android support library annotations (@LayoutRes) in this project.
// Since this isn't an android module normally we couldn't access them otherwise.
// Taken from https://github.com/JakeWharton/butterknife/pull/380
def logger = new LoggerWrapper(project.logger)
def sdkHandler = new SdkHandler(project, logger)
for (File file : sdkHandler.sdkLoader.repositories) {
    repositories.maven {
        url = file.toURI()
    }
}

dependencies {
    implementation deps.kotlin
    implementation deps.squareJavaPoet
    implementation deps.squareKotlinPoet
    implementation deps.androidAnnotations
    implementation "me.eugeniomarletti.kotlin.metadata:kotlin-metadata:1.4.0"

    /** Provides the sun javac tools for looking up the R class trees. */
    compileOnly files(Jvm.current().getToolsJar())

    implementation project(':epoxy-annotations')

    testImplementation rootProject.deps.junit
}

checkstyle {
    configFile rootProject.file('checkstyle.xml')
    showViolations true
}

apply from: rootProject.file('gradle/gradle-maven-push.gradle')

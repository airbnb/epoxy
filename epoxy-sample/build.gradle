apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'

buildscript {
  repositories {
    mavenCentral()
  }

  dependencies {
    classpath 'me.tatarka:gradle-retrolambda:3.5.0'
  }
}

repositories {
  // For retrolambda
  mavenCentral()
}

android {
  compileSdkVersion rootProject.COMPILE_SDK_VERSION
  buildToolsVersion rootProject.ANDROID_BUILD_TOOLS_VERSION

  // For retrolambda
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  defaultConfig {
    applicationId "com.airbnb.android.epoxysample"
    minSdkVersion rootProject.MIN_SDK_VERSION
    targetSdkVersion rootProject.TARGET_SDK_VERSION
    versionCode 1
    versionName "1.0"
  }

  buildTypes {
    debug {
      buildConfigField "boolean", "VALIDATE_AUTO_MODEL_USAGE", "true"
    }

    release {
      buildConfigField "boolean", "VALIDATE_AUTO_MODEL_USAGE", "false"
    }
  }
}

/* Forces all transitive deps listed to our version. */
configurations.all { strategy ->
  strategy.resolutionStrategy.force rootProject.deps.androidCardView, rootProject.deps.junit
}

dependencies {
  compile project(':epoxy-adapter')
  compile project(':epoxy-annotations')
  annotationProcessor project(':epoxy-processor')

  compile rootProject.deps.butterknifeLibrary
  annotationProcessor rootProject.deps.butterknifeCompiler

  compile rootProject.deps.androidCardView

  testCompile rootProject.deps.junit
}

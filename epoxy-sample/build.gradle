apply plugin: 'com.android.application'

android {
  compileSdkVersion rootProject.COMPILE_SDK_VERSION

  // For java 8 lambdas
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  defaultConfig {
    applicationId "com.airbnb.android.epoxysample"
    minSdkVersion 16
    targetSdkVersion rootProject.TARGET_SDK_VERSION
    vectorDrawables.useSupportLibrary = true
    versionCode 1
    versionName "1.0"
  }

  dataBinding {
    enabled = true
  }
}

project.android.buildTypes.all { buildType ->
  buildType.javaCompileOptions.annotationProcessorOptions.arguments =
      [
          validateEpoxyModelUsage     : String.valueOf(buildType.name == 'debug'),
          requireHashCodeInEpoxyModels: "true",
          requireAbstractEpoxyModels  : "true",
          implicitlyAddAutoModels     : "true"
      ]
}

/* Forces all transitive deps listed to our version. */
configurations.all { strategy ->
  strategy.resolutionStrategy.force rootProject.deps.androidCardView, rootProject.deps.junit
}

dependencies {
  implementation project(':epoxy-databinding')
  implementation project(':epoxy-adapter')
  implementation project(':epoxy-annotations')
  annotationProcessor project(':epoxy-processor')

  implementation rootProject.deps.paris
  annotationProcessor rootProject.deps.parisProcessor

  // Dependencies for the optional litho integration
    //
    //
  implementation project(':epoxy-litho')
  annotationProcessor rootProject.deps.lithoProcessor
  implementation rootProject.deps.lithoCore
  implementation rootProject.deps.lithoWidget
  implementation rootProject.deps.lithoSoloader
  provided rootProject.deps.lithoAnnotations

  implementation rootProject.deps.butterknifeLibrary
  annotationProcessor rootProject.deps.butterknifeCompiler

  implementation rootProject.deps.androidCardView
  implementation rootProject.deps.androidRecyclerView
  implementation rootProject.deps.androidAppcompat
  implementation rootProject.deps.androidLegacy
  implementation rootProject.deps.lottie

  implementation "com.android.support:support-annotations:27.1.1"

  testImplementation rootProject.deps.junit
}
